if (WIN32)
  set(_find_triplet "x64-windows")
endif()
if (UNIX)
  set(_find_triplet "x64-linux")
endif()

list(APPEND CMAKE_PREFIX_PATH 
#   "${CMAKE_BINARY_DIR}/vcpkg_installed/${_find_triplet}/lib/pkgconfig"
  "${CMAKE_BINARY_DIR}/vcpkg_installed/${_find_triplet}/tools"
)

if (NOT WIN32)
  find_package(PkgConfig REQUIRED)
endif()

function(find_package_vcpkg _opts_ALIAS _opts_VCPKG)
  if (WIN32)
    set(VCPKG_DEFAULT_TRIPLET "x64-windows")
  endif()
  if (UNIX)
    set(VCPKG_DEFAULT_TRIPLET "x64-linux")
  endif()
  
  find_package(${_opts_VCPKG} CONFIG REQUIRED HINTS ${CMAKE_BINARY_DIR}/vcpkg_installed/${VCPKG_DEFAULT_TRIPLET}/share/${opts_VCPKG}) # NAMES ${opts_VCPKG} )
  if (NOT ${_opts_VCPKG}_FOUND)
    message(FATAL_ERROR "(vcpkg[${_opts_VCPKG}]): NOT FOUND ${_opts_ALIAS} ${CMAKE_BINARY_DIR}/vcpkg_installed/${VCPKG_DEFAULT_TRIPLET}/share/${opts_VCPKG}")
  else()
    if (NOT TARGET ${_opts_ALIAS})
      message(STATUS "@vcpkg: ${_opts_ALIAS}")
      add_library(${_opts_ALIAS} INTERFACE IMPORTED)
      set_property(TARGET ${_opts_ALIAS} PROPERTY INTERFACE_LINK_LIBRARIES ${ARGN})
      set_property(TARGET ${_opts_ALIAS} PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${CMAKE_BINARY_DIR}/vcpkg_installed/${VCPKG_DEFAULT_TRIPLET}/include)
    else()
      message(STATUS "(vcpkg): ${_opts_ALIAS} already exists... not redefining")
    endif()
  endif()
endfunction()

function(find_package_pkgconfig _opts_ALIAS _opts_PKG)
  # message(STATUS "find_package_pkgconfig(${_opts_ALIAS} ${_opts_PKG} ${ARGN})")
  #list(PREPEND ARGN QUIET)
  pkg_check_modules(${_opts_PKG} ${ARGN})
  
  if (_pkgconfig_error)
    message(STATUS "(pkg-config[${_opts_PKG}]): NOT FOUND ${_opts_ALIAS} ${_pkgconfig_error}")
  else()
    # message(STATUS "(pkg-config): FOUND ${_opts_ALIAS} ${PKG_CONFIG_FOUND}")
    if (NOT TARGET ${_opts_ALIAS})
      message(STATUS "@pkg-config[${_opts_PKG}]: ${_opts_ALIAS} include:${${_opts_PKG}_INCLUDE_DIRS} libs: ${${_opts_PKG}_LIBRARIES} link: ${${_opts_PKG}_LINK_LIBRARIES}")
      add_library(${_opts_ALIAS} INTERFACE IMPORTED)
      if(${_opts_PKG}_INCLUDE_DIRS)
        set_property(TARGET ${_opts_ALIAS} PROPERTY INTERFACE_INCLUDE_DIRECTORIES "${${_opts_PKG}_INCLUDE_DIRS}")
      endif()
      if(${_opts_PKG}_LINK_LIBRARIES)
        set_property(TARGET ${_opts_ALIAS} PROPERTY INTERFACE_LINK_LIBRARIES "${${_opts_PKG}_LINK_LIBRARIES}")
      endif()
      if(${_opts_PKG}_LDFLAGS_OTHER)
        set_property(TARGET ${_opts_ALIAS} PROPERTY INTERFACE_LINK_OPTIONS "${${_opts_PKG}_LDFLAGS_OTHER}")
      endif()
      if(${_opts_PKG}_CFLAGS_OTHER)
        set_property(TARGET ${_opts_ALIAS} PROPERTY INTERFACE_COMPILE_OPTIONS "${${_opts_PKG}_CFLAGS_OTHER}")
      endif()    
    else()
      message(STATUS "(pkg-config[${_opts_PKG}]): ${_opts_ALIAS} already exists... not redefining")
    endif()
  endif()
endfunction()

function(find_package_system_first _opts_ALIAS)
    set(options FALLBACK)
    set(oneValueArgs VCPKG PKG)
    set(multiValueArgs PKG_MODULES VCPKG_MODULES)
    cmake_parse_arguments(_opts "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})  
    if (WIN32 OR NOT _opts_PKG OR USE_VCPKG)
      find_package_vcpkg(${_opts_ALIAS} ${_opts_VCPKG} ${_opts_VCPKG_MODULES})
    else()
      set(_opts_args "")
      if (NOT _opts_FALLBACK)
        list(APPEND _opts_args REQUIRED)
      endif()
      list(APPEND _opts_args ${_opts_PKG_MODULES})
      find_package_pkgconfig(${_opts_ALIAS} ${_opts_PKG} ${_opts_args})
      if (NOT TARGET ${_opts_ALIAS})
        if (_opts_FALLBACK)
          message(STATUS "(pkg-config[${_opts_PKG}]): fallilng back to VCPKG...")
          unset(${_opts_PKG}_FOUND CACHE)
          find_package_vcpkg(${_opts_ALIAS} ${_opts_VCPKG} ${_opts_VCPKG_MODULES})
        endif()
      endif()
      # message(STATUS "calling pkg_check_modules ${_opts_PKG} ${_opts_REQUIRED}")
    endif()
endfunction()

